prefix dwc: <http://rs.tdwg.org/dwc/terms/>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix dc: <http://purl.org/dc/elements/1.1/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ppo: <http://www.plantphenology.org/id/>
prefix urn: <urn:>

# Use the concat/group_concat function to push all plantStructurePresence types into an array within a field.
# this enables ElasticSearch to index this easily while shrinking output file sizes
SELECT  (?phenologyObservingProcess as ?eventId) ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?basisOfRecord ?lower_count_partplant ?upper_count_partplant ?lower_count_wholeplant ?upper_count_wholeplant ?lower_percent_wholeplant ?upper_percent_wholeplant ?source ?subSource (group_concat(distinct ?partPlantTraitType;separator='|') as ?partPlantStructurePresenceTypes) (group_concat(distinct ?wholePlantTraitType;separator='|') as ?plantStructurePresenceTypes)
#SELECT  (?phenologyObservingProcess as ?eventId) ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?basisOfRecord ?lower_count ?upper_count ?lower_percent ?upper_percent ?source ?subSource (group_concat(distinct ?partPlantTraitType;separator='|') as ?partPlantStructurePresenceTypes) 

WHERE {    

        ?plant dwc:genus ?genus . 

        OPTIONAL {?plant dwc:specificEpithet ?specificEpithet} . 
        OPTIONAL {?plant dwc:scientificName ?scientificName} . 
        OPTIONAL {?plant dwc:basisOfRecord ?basisOfRecord} . 

	# Join plantStructurePresence to plant
        ?partPlantStructurePresence obo:RO_0000080 ?plant .

	# Setup queries for the rdf:type of the plantStructurePresence (trait type) category
        ?partPlantStructurePresence rdf:type ?partPlantTraitType .
        ?wholePlantStructurePresence rdf:type ?wholePlantTraitType .

	# remove declarations of owl:namedIndividual
	FILTER (?partPlantTraitType != <http://www.w3.org/2002/07/owl#NamedIndividual>) .
	FILTER (?wholePlantTraitType != <http://www.w3.org/2002/07/owl#NamedIndividual>) .
	#FILTER (?typePlant != <http://www.w3.org/2002/07/owl#NamedIndividual>) .

	# Here we use a property path to get the wholePlantStructurePresence in the context of 
	# part plant data.
	# TODO: check this works for whole plant data
        # partPlant
        ?partPlantStructurePresence obo:PPO_0000008 ?measurementDatum .
	# wholePlant
        ?wholePlantStructurePresence obo:PPO_0000008/obo:PPO_0000014 ?wpMeasurementDatum .

	# Join phenologyObservingProcess to measurementDatum
        ?measurementDatum obo:OBI_0000312 ?phenologyObservingProcess .
	# JBD: Here on september 12th... need another property property path to grab the wpMeasurementDatum
        ?wpMeasurementDatum obo:PPO_0000014/obo:OBI_0000312 ?phenologyObservingProcess .

        # Set the type for phenologyObservingProcess and return properties
        ?phenologyObservingProcess rdf:type obo:PPO_0007004 .
        ?phenologyObservingProcess dwc:startDayOfYear ?dayOfYear .
        ?phenologyObservingProcess dwc:year ?year .
        ?phenologyObservingProcess dwc:decimalLatitude ?latitude .
        ?phenologyObservingProcess dwc:decimalLongitude ?longitude .
	OPTIONAL {?phenologyObservingProcess dc:creator ?source} .
	OPTIONAL {?phenologyObservingProcess urn:subSource ?subSource} .

	# Return properties of measurementDatum
        OPTIONAL {?measurementDatum obo:PPO_0000001 ?lower_count_partplant} .
        OPTIONAL {?measurementDatum obo:PPO_0000002 ?upper_count_partplant} .
        OPTIONAL {?wpMeasurementDatum obo:PPO_0000001 ?lower_count_wholeplant} .
        OPTIONAL {?wpMeasurementDatum obo:PPO_0000002 ?upper_count_wholeplant} .
        OPTIONAL {?wpMeasurementDatum obo:PPO_0000003 ?lower_percent_wholeplant} .
        OPTIONAL {?wpMeasurementDatum obo:PPO_0000004 ?upper_percent_wholeplant} .
}
GROUP BY ?phenologyObservingProcess ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?basisOfRecord ?lower_count_partplant ?upper_count_partplant ?lower_count_wholeplant ?upper_count_wholeplant ?lower_percent_wholeplant ?upper_percent_wholeplant ?source ?subSource 
ORDER BY ASC(?phenologyObservingProcess)


